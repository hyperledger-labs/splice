-- Copyright (c) 2024 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

-- | An example of how to build an OTC trading app for multi-leg standard token trades.
--
-- Used as part of the testing infrastructure to test the DvP workflows based on the token standard.
module Splice.Testing.Apps.TradingApp where

import DA.Foldable qualified as F
import DA.Optional (fromOptional, fromSomeNote)
import DA.Set as Set
import DA.TextMap as TextMap
import DA.Traversable qualified as Traversable

import Splice.Api.Token.MetadataV1 as Api.Token.MetadataV1
import Splice.Api.Token.AllocationV1 as Api.Token.AllocationV1
import Splice.Api.Token.AllocationRequestV1


template OTCTradeProposal with
    venue : Party
    tradeCid : Optional (ContractId OTCTradeProposal) -- Tracking-id for the trade being proposed. Set to None for new trades.
    transferLegs : TextMap Api.Token.AllocationV1.TransferLeg
    approvers : [Party] -- ^ Parties that have approved the proposal
  where
    signatory approvers
    observer venue, tradingParties transferLegs

    -- This is test code, so we don't care about the contention here.
    -- Moreover, likely the number of trading parties is going to be low anyways.
    choice OTCTradeProposal_Accept : ContractId OTCTradeProposal
      with
        approver : Party
      controller approver
      do
        let newApprovers = approver :: approvers
        let traders = tradingParties transferLegs
        require "Approver is a trading party" (approver `Set.member` traders)
        require "Approver is new" (approver `notElem` approvers)
        create this with
          approvers = newApprovers
          tradeCid = Some (fromOptional self tradeCid)

    choice OTCTradeProposal_Reject : ()
      with
        trader : Party
      controller trader
      do require "Trader is a trading party" (trader `Set.member` tradingParties transferLegs)

    choice OTCTradeProposal_InitiateSettlement : ContractId OTCTrade
      with
        prepareUntil : Time
        settleBefore : Time
      controller venue
      do
        require "All trading parties have approved" (Set.fromList approvers == tradingParties transferLegs)
        now <- getTime
        require "Preparation time has not passed" (now < prepareUntil)
        require "Preparation time before settlement time" (prepareUntil < settleBefore)
        create OTCTrade with
          venue
          transferLegs
          tradeCid = fromOptional self tradeCid
          createdAt = now
          prepareUntil
          settleBefore

tradeAllocations
  : SettlementInfo -> TextMap Api.Token.AllocationV1.TransferLeg
  -> TextMap AllocationSpecification
tradeAllocations settlementInfo transferLegs =
  TextMap.fromList $ do
    (transferLegId, transferLeg) <- TextMap.toList transferLegs
    let spec = AllocationSpecification with
          settlement = settlementInfo
          transferLegId
          transferLeg
    pure (transferLegId, spec)

template OTCTrade
  with
    venue : Party
    transferLegs : TextMap Api.Token.AllocationV1.TransferLeg
    tradeCid : ContractId OTCTradeProposal
    createdAt : Time
    prepareUntil : Time
    settleBefore : Time
  where
    signatory venue, tradingParties transferLegs

    choice OTCTrade_Settle : TextMap Allocation_ExecuteTransferResult
      with
        allocationsWithContext : TextMap (ContractId Allocation, ExtraArgs)
      controller venue
      do
        -- check timing constraints
        now <- getTime
        require "Settlement deadline has not passed" (now < settleBefore)
        -- validate and execute transferLegs
        let settlementInfo = SettlementInfo with
              executor = venue
              requestedAt = createdAt
              settlementRef = makeTradeRef tradeCid
              allocateBefore = prepareUntil
              settleBefore
              meta = emptyMetadata
        let expectedAllocations = tradeAllocations settlementInfo transferLegs
        let mergedMaps = zipTextMaps allocationsWithContext expectedAllocations
        forTextMapWithKey mergedMaps \legId (optAllocWithContext,  optExpectedAlloc) -> do
          let (allocCid, extraArgs) = fromSomeNote ("Allocation cid and extra arg is missing for leg " <> legId) optAllocWithContext
          let expectedAlloc = fromSomeNote ("Allocation with context provided for unexpected leg " <> legId) optExpectedAlloc
          -- fetch and validate the allocation instruction
          instr <- fetch @Allocation allocCid
          let instrView = view @Allocation instr
          require "Allocation matches expected allocation" (instrView.allocation == expectedAlloc)
          exercise allocCid (Allocation_ExecuteTransfer extraArgs)


    -- NOTE: this choice is an approximation to what a real app would do.
    -- As it stands, the venue can't cancel allocations that come right after
    -- the first cancellation.  A better approach would be to leave a marker
    -- contract in place until the `settleBefore` time, so that the venue
    -- retains the ability to cancel the allocations that are created.
    choice OTCTrade_Cancel : TextMap (Optional Allocation_CancelResult)
      with
        allocationsWithContext : TextMap (ContractId Allocation, ExtraArgs)
      controller venue
      do
        -- validate and cancel transferLegs
        let settlementInfo = SettlementInfo with
              executor = venue
              requestedAt = createdAt
              settlementRef = makeTradeRef tradeCid
              allocateBefore = prepareUntil
              settleBefore
              meta = emptyMetadata
        let expectedAllocations = tradeAllocations settlementInfo transferLegs
        let mergedMaps = zipTextMaps allocationsWithContext expectedAllocations
          -- fetch and validate the allocation instruction
        forTextMapWithKey mergedMaps \legId (optAllocWithContext,  optExpectedAlloc) ->
          -- skip the leg if there is no matching allocation to cancel
          Traversable.forA optAllocWithContext $ \(allocCid, extraArgs) -> do
            -- fetch and validate the allocation instruction
            let expectedAlloc = fromSomeNote ("Allocation with context provided for unexpected leg " <> legId) optExpectedAlloc
            instr <- fetch @Allocation allocCid
            let instrView = view @Allocation instr
            require "Allocation matches expected allocation" (instrView.allocation == expectedAlloc)
            exercise allocCid (Allocation_Cancel extraArgs)


    interface instance AllocationRequest for OTCTrade where
      view = AllocationRequestView with
        settlement = SettlementInfo with
          executor = venue
          requestedAt = createdAt
          settlementRef = makeTradeRef tradeCid
          allocateBefore = prepareUntil
          settleBefore
          meta = emptyMetadata
        transferLegs
        meta = emptyMetadata

      allocationRequest_RejectImpl _self AllocationRequest_Reject{..} = do
        -- Note: this corresponds to signalling early that one is going to fail to deliver one's assets.
        -- A real trading app will likely demand punitive charges for this.
        require "Actor is a sender" (F.any (\leg -> actor == leg.sender) transferLegs)
        pure ChoiceExecutionMetadata with meta = emptyMetadata

      allocationRequest_WithdrawImpl _self _extraArgs =
        -- just archiving the trade is enough
        pure ChoiceExecutionMetadata with meta = emptyMetadata


tradingParties : TextMap Api.Token.AllocationV1.TransferLeg -> Set.Set Party
tradingParties = F.foldl (\acc t -> Set.insert t.sender (Set.insert t.receiver acc)) Set.empty

-- | Check whether a required condition is true. If it's not, abort the
-- transaction with a message saying that the requirement was not met.
require : CanAssert m => Text -> Bool -> m ()
require msg invariant =
  assertMsg ("The requirement '" <> msg <> "' was not met.") invariant

makeTradeRef : ContractId OTCTradeProposal -> Api.Token.AllocationV1.Reference
makeTradeRef tradeCid = Api.Token.AllocationV1.Reference with
  id = "OTCTradeProposal" -- set to the name of the template to simplify debugging
  cid = Some (coerceContractId tradeCid)


-- Additional text map utilities
--------------------------------

zipTextMaps : TextMap a -> TextMap b -> TextMap (Optional a, Optional b)
zipTextMaps m1 m2 =
  TextMap.merge
    (\_ v1 -> Some (Some v1, None))
    (\_ v2 -> Some (None, Some v2))
    (\_ v1 v2 -> Some (Some v1, Some v2))
    m1
    m2

forTextMapWithKey : Applicative f => TextMap a -> (Text -> a -> f b) -> f (TextMap b)
forTextMapWithKey m f =
    TextMap.fromList <$> mapA f' (TextMap.toList m)
  where
    f' (k, v) = (k,) <$> f k v
