-- Copyright (c) 2024 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Splice.Scripts.TestOnboarding where

import DA.Assert
import DA.Foldable (forA_)
import DA.Optional (fromSome)
import qualified DA.Map as Map
import Daml.Script
import DA.Time

import Splice.ValidatorLicense

import Splice.CometBft
import Splice.DsoRules
import Splice.DSO.DecentralizedSynchronizer
import Splice.SvOnboarding

import Splice.Scripts.Util
import Splice.Scripts.DsoTestUtils
import Splice.AmuletRules (AmuletRules)
import Splice.Types


testOnboardValidator : Script ()
testOnboardValidator = do
  (app, dso, (_, sv2, _, _)) <- initMainNet

  -- a new validator prepares his participant
  v1 <- allocateParty "v1"

  now <- getTime

  -- sv2 (not the DSO delegate) onboards the new validator
  onboardValidator app sv2 "v1" v1

  -- v1 received a validator license
  [(validatorLicenseCid, validatorLicense)] <- queryFilter @ValidatorLicense dso (\license -> license.validator == v1)
  validatorLicense === ValidatorLicense with
    validator = v1
    sponsor = sv2
    dso
    faucetState = None
    metadata = Some ValidatorLicenseMetadata with
      version = "0.1.0"
      contactPoint = "v1@example.com"
      lastUpdatedAt = now
    lastActiveAt = Some now
    weight = None
    kind = Some OperatorLicense

  -- so v1 is onboarded!

  -- v1 can update its version and contact point

  -- fails without advancing the time
  submitMultiMustFail [v1] [dso] $ exerciseCmd validatorLicenseCid ValidatorLicense_UpdateMetadata with
    version = "0.2.0"
    contactPoint = "v1@example2.com"

  passTime (hours 1)
  metadataUpdateTime <- getTime

  -- works after advancing time
  submitMulti [v1] [dso] $ exerciseCmd validatorLicenseCid ValidatorLicense_UpdateMetadata with
    version = "0.2.0"
    contactPoint = "v1@example2.com"

  -- license is updated
  [(validatorLicenseCid, validatorLicense)] <- queryFilter @ValidatorLicense dso (\license -> license.validator == v1)
  validatorLicense === ValidatorLicense with
    validator = v1
    sponsor = sv2
    dso
    faucetState = None
    metadata = Some ValidatorLicenseMetadata with
      lastUpdatedAt = metadataUpdateTime
      version = "0.2.0"
      contactPoint = "v1@example2.com"
    lastActiveAt = Some metadataUpdateTime
    weight = None
    kind = Some OperatorLicense


  -- activity cannot be reported without advancing time
  submitMultiMustFail [v1] [dso] $ exerciseCmd validatorLicenseCid ValidatorLicense_ReportActive

  passTime (hours 1)
  now <- getTime

  submitMulti [v1] [dso] $ exerciseCmd validatorLicenseCid ValidatorLicense_ReportActive
  [(_, validatorLicense)] <- queryFilter @ValidatorLicense dso (\license -> license.validator == v1)
  validatorLicense === ValidatorLicense with
    validator = v1
    sponsor = sv2
    dso
    faucetState = None
    metadata = Some ValidatorLicenseMetadata with
      lastUpdatedAt = metadataUpdateTime
      version = "0.2.0"
      contactPoint = "v1@example2.com"
    lastActiveAt = Some now
    weight = None
    kind = Some OperatorLicense
  pure ()

testSvOnboarding : Script ()
testSvOnboarding = do
  (app, dso, (_, sv2, sv3, sv4)) <- initMainNet
  -- we now have enough SVs to achieve BFT for f = 1

  -- a new sv prepares his participant
  svX <- allocateParty "svX"

  -- we can't test token validity in Daml (it's a JWT)
  let token = "mock-token"

  -- sv2 (not the DSO delegate) initiates the onboarding of the new sv
  svSubmits app sv2 $ \cid -> exerciseCmd cid $
    DsoRules_StartSvOnboarding "svX" svX "svX-participant-id" token sv2

  -- an onboarding request for svX has been created
  [(_, svOnboardingRequest)] <- query @SvOnboardingRequest dso
  svOnboardingRequest === SvOnboardingRequest with
    candidateName = "svX"
    candidateParty = svX
    candidateParticipantId = "svX-participant-id"
    token
    sponsor = sv2
    dso
    expiresAt = svOnboardingRequest.expiresAt

  -- svX collects three confirmations which is enough to onboard him
  [(dsoRulesCid, _)] <- query @DsoRules dso
  forA_ [sv2, sv3, sv4] $ \sv ->
    submitMulti [sv] [dso] $ exerciseCmd dsoRulesCid DsoRules_ConfirmAction with
      confirmer = sv
      action = ARC_DsoRules with
        dsoAction = SRARC_ConfirmSvOnboarding $ DsoRules_ConfirmSvOnboarding with
          newSvParty = svX
          newSvName = "svX"
          newParticipantId = "svX-participant-id"
          reason = "because"
          newSvRewardWeight = 19 -- we use 19 as a dummy reward weight for testing
  executeAllConfirmedActions app

  -- svX is now a confirmed SV
  [(svOnboardingConfirmedCid, svOnboardingConfirmed)] <- query @SvOnboardingConfirmed dso
  svOnboardingConfirmed === SvOnboardingConfirmed with
    svParty = svX
    svName = "svX"
    svParticipantId = "svX-participant-id"
    reason = "because"
    svRewardWeight = 19
    dso
    expiresAt = svOnboardingConfirmed.expiresAt

  -- once svX becomes able to read as DSO party (e.g., after party migration to svX's participant)...
  [(dsoRulesCid, _)] <- query @DsoRules dso

  -- it gets the open mining rounds, so that it can set it's own `joinedAsOfRound` in a cheat-resistant way
  [(earliestRoundCid, earliestRound), (middleRoundCid, _), (latestRoundCid, _)] <- getActiveOpenRoundsSorted app

  [(amuletRulesCid, _)] <- query @AmuletRules dso

  -- the rounds need to be passed in the correct order
  submitMultiMustFail [svX] [dso] $ exerciseCmd dsoRulesCid DsoRules_AddConfirmedSv with
    sv = svX
    svOnboardingConfirmedCid
    earliestRoundCid = latestRoundCid
    middleRoundCid
    latestRoundCid = earliestRoundCid
    amuletRulesCid
  submitMultiMustFail [svX] [dso] $ exerciseCmd dsoRulesCid DsoRules_AddConfirmedSv with
    sv = svX
    svOnboardingConfirmedCid
    earliestRoundCid
    middleRoundCid = earliestRoundCid
    latestRoundCid = earliestRoundCid
    amuletRulesCid

  -- onboarding fails if the time expired
  now <- getTime
  setTime svOnboardingConfirmed.expiresAt

  submitMultiMustFail [svX] [dso] $ exerciseCmd dsoRulesCid DsoRules_AddConfirmedSv with
    sv = svX
    svOnboardingConfirmedCid
    earliestRoundCid
    middleRoundCid
    latestRoundCid
    amuletRulesCid

  -- reset the time to now
  setTime now

  -- svX completes its own onboarding by adding itself to the DsoRules
  submitMulti [svX] [dso] $ exerciseCmd dsoRulesCid DsoRules_AddConfirmedSv with
    sv = svX
    svOnboardingConfirmedCid
    earliestRoundCid
    middleRoundCid
    latestRoundCid
    amuletRulesCid

  checkSvContractInvariants app

  [(_, dsoRules)] <- query @DsoRules dso
  case Map.lookup svX dsoRules.svs of
    None -> fail "svX is not an SV"
    Some info ->
      -- the new sv joined in the earliest open round
      info.joinedAsOfRound === earliestRound.round
  now <- getTime
  -- now that svX is a sv it can set its CometBFT config to become a CometBFT  validator
  -- TODO(M3-59): add negative tests for limits enforced on config values (manual testing showed they work)
  let cometBftConfig = CometBftConfig with
        nodes = Map.fromList
          [ ( "svX-node-id-1"
            , CometBftNodeConfig with
                validatorPubKey = "==base-64-validator-pub-key-1"
                votingPower = 0
            )
          , ( "svX-node-id-2"
            , CometBftNodeConfig with
                validatorPubKey = "==base-64-validator-pub-key-1"
                votingPower = 1
            )
          ]
        governanceKeys = [GovernanceKeyConfig with pubKey = "==base-64-pubKey-1"]
        sequencingKeys = []

  let sequencerConfig = Some SequencerConfig with
        migrationId = 0
        sequencerId = "sequencer-id"
        url = "sequencer.address:1234"
        availableAfter = Some now

  let mediatorConfig = Some MediatorConfig with
        mediatorId = "mediator-id"

  let scanConfig = Some ScanConfig with
        publicUrl = "https://scan1234.example.com"

  let newNodeConfig = SynchronizerNodeConfig with
        cometBft = cometBftConfig
        sequencer = sequencerConfig
        mediator = mediatorConfig
        scan = scanConfig
        legacySequencerConfig = None

  setSynchronizerNodeConfig app svX dsoRules.config.decentralizedSynchronizer.lastSynchronizerId newNodeConfig

  -- check that the config was actually set
  (_, nodeState) <- getSvNodeState app svX
  let actualConfig =
        Map.lookup dsoRules.config.decentralizedSynchronizer.lastSynchronizerId nodeState.state.synchronizerNodes
  actualConfig === Some newNodeConfig

  pure ()


testSvOnboardingRequestExpiry : Script ()
testSvOnboardingRequestExpiry = do
  (app, _, (sv1, sv2, _, _)) <- initMainNet

  -- a new sv prepares his participant
  svX <- allocateParty "svX"

  -- we can't test token validity in Daml (it's a JWT)
  let token = "mock-token"

  -- sv2 (not the DSO delegate) initiates the onboarding of the new sv
  result <- svSubmits app sv2 $ \cid -> exerciseCmd cid $
    DsoRules_StartSvOnboarding "svX" svX "svX-participant-id" token sv2
  let svOnboardingRequestCid = result.onboardingRequest

  -- DSO delegate tries to expire the onboarding, but it's too early
  dsoDelegateSubmitsMustFail app $ \cid -> exerciseCmd cid $
    DsoRules_ExpireSvOnboardingRequest svOnboardingRequestCid (Some sv1)

  -- wait for the onboarding to expire
  passTime (hours 25)

  -- DSO delegate expires the onboarding
  dsoDelegateSubmits app $ \cid -> exerciseCmd cid $
    DsoRules_ExpireSvOnboardingRequest svOnboardingRequestCid (Some sv1)

  pure ()


testSvOnboardingRequestArchiveWhenSvIsAlreadyAddedAsSV : Script ()
testSvOnboardingRequestArchiveWhenSvIsAlreadyAddedAsSV = do
  (app, dso, (sv1, sv2, _, _)) <- initMainNet

  -- a new sv prepares his participant
  svX <- allocateParty "svX"

  -- we can't test token validity in Daml (it's a JWT)
  let token = "mock-token"

  -- sv2 (not the DSO delegate) initiates the onboarding of the new sv
  onboardingResult <- svSubmits app sv2 $ \cid -> exerciseCmd cid $
    DsoRules_StartSvOnboarding "svX" svX "svx-participant-id" token sv2
  let svOnboardingRequestCid = onboardingResult.onboardingRequest

  [(dsoRulesCid, _)] <- query @DsoRules dso

  -- DSO delegate cannot archive the onboarding as svX is not yet added as sv
  dsoDelegateSubmitsMustFail app $ \cid -> exerciseCmd cid $
    DsoRules_ArchiveSvOnboardingRequest svOnboardingRequestCid (Some sv1)

  -- create a SvOnboardingConfirmed contract
  submit dso $ exerciseCmd dsoRulesCid $ DsoRules_AddSv with
    newSvParty = svX
    newSvName = "svX"
    newSvRewardWeight = 19
    newSvParticipantId = "svX-participant-id"
    joinedAsOfRound = (Round 3)

  -- DSO delegate can archive the onboarding as svX is added as sv
  dsoDelegateSubmits app $ \cid -> exerciseCmd cid $
    DsoRules_ArchiveSvOnboardingRequest svOnboardingRequestCid (Some sv1)

  -- check status report invaraints
  checkSvContractInvariants app


testSvOnboardingConfirmedExpiry : Script ()
testSvOnboardingConfirmedExpiry = do
  (app, dso, (sv1, sv2, sv3, sv4)) <- initMainNet

  -- a new sv prepares his participant
  svX <- allocateParty "svX"

  -- svX collects three confirmations which is enough to onboard him
  [(dsoRulesCid, _)] <- query @DsoRules dso
  forA_ [sv2, sv3, sv4] $ \sv ->
    submitMulti [sv] [dso] $ exerciseCmd dsoRulesCid DsoRules_ConfirmAction with
      confirmer = sv
      action = ARC_DsoRules with
        dsoAction = SRARC_ConfirmSvOnboarding $ DsoRules_ConfirmSvOnboarding with
          newSvParty = svX
          newSvName = "svX"
          newParticipantId = "svX-participant-id"
          newSvRewardWeight = 19
          reason = "because"

  executeAllConfirmedActions app

  [(svOnboardingConfirmedCid, _)] <- query @SvOnboardingConfirmed dso

  -- DSO delegate tries to expire the confirmation but it's too early
  dsoDelegateSubmitsMustFail app $ \cid -> exerciseCmd cid $
    DsoRules_ExpireSvOnboardingConfirmed svOnboardingConfirmedCid (Some sv1)

  -- wait for the confirmation to expire
  passTime (hours 25)

  -- DSO delegate expires the confirmation
  _ <- dsoDelegateSubmits app $ \cid -> exerciseCmd cid $
    DsoRules_ExpireSvOnboardingConfirmed with
        cid = svOnboardingConfirmedCid
        sv = Some sv1

  [(_, dsoRules)] <- query @DsoRules dso
  Map.keys (dsoRules.offboardedSvs) === [svX]


testSvNameUniquenessNotInDevNet : Script ()
testSvNameUniquenessNotInDevNet = do
  -- When it is not in devnet, a new SV party cannot dynamically replace an existing SV party for the same SV name.
  (_, dso, (_, sv2, _, _)) <- initMainNet

  [(dsoRulesCid, dsoRules)] <- query @DsoRules dso

  (fromSome (Map.lookup sv2 dsoRules.svs)).name === "sv2"

  svX <- allocateParty "svX"
  submitMustFail dso $ exerciseCmd dsoRulesCid $ DsoRules_AddSv svX "sv2" 19 "sv2-participant-id" (Round 3)


testSvOverwriteInDevNet : Script ()
testSvOverwriteInDevNet = do
  (app, dso, (sv1, sv2, _, _)) <- initDevNet

  [(dsoRulesCid, dsoRules)] <- query @DsoRules dso

  (fromSome (Map.lookup sv1 dsoRules.svs)).name === "sv1"
  (fromSome (Map.lookup sv2 dsoRules.svs)).name === "sv2"
  svX <- allocateParty "svX"

  -- overwriting existing sv successfully (using a new participant node)
  submit dso $ exerciseCmd dsoRulesCid $ DsoRules_AddSv svX "sv2" 19 "svX-participant-id" (Round 3)

  [(_, dsoRules)] <- query @DsoRules app.dso
  dsoRules.offboardedSvs === Map.fromList [(sv2, OffboardedSvInfo "sv2" "sv2-participant-id")]

  svX2 <- allocateParty "svX2"

  [(dsoRulesCid, _)] <- query @DsoRules app.dso

  -- overwriting existing sv again
  submit dso $ exerciseCmd dsoRulesCid $ DsoRules_AddSv svX2 "sv2" 19 "svX2-participant-id" (Round 3)

  [(_, dsoRules)] <- query @DsoRules app.dso
  dsoRules.offboardedSvs === Map.fromList
    [ (sv2, OffboardedSvInfo "sv2" "sv2-participant-id")
    , (svX, OffboardedSvInfo "sv2" "svX-participant-id")
    ]

  -- status reports are OK
  checkSvContractInvariants app

  pure ()

testSvReonboarding : Script ()
testSvReonboarding = do
  (app, dso, (sv1, sv2, sv3, sv4)) <- initMainNet

  [(_, dsoRules)] <- query @DsoRules dso

  Map.keys dsoRules.svs === [sv1, sv2, sv3, sv4]

  initiateAndAcceptVote app [sv1, sv2, sv3] $
    ARC_DsoRules with
      dsoAction = SRARC_OffboardSv DsoRules_OffboardSv with
        sv = sv4
  passTime (dsoRules.config.voteRequestTimeout)
  executeAllDefinitiveVotes app

  [(_, dsoRules)] <- query @DsoRules dso
  Map.keys dsoRules.svs === [sv1, sv2, sv3]
  Map.keys dsoRules.offboardedSvs === [sv4]
  checkSvContractInvariants app

  -- reonboard sv4
  let token = "mock-token"
  sv4New <- allocateParty "sv4New"

  svSubmits app sv1 $ \cid -> exerciseCmd cid $
    DsoRules_StartSvOnboarding "sv4" sv4New "sv4New-participant-id" token sv1
  forA_ [sv1,sv2,sv3] $ \sv ->
    svSubmits app sv $ \cid -> exerciseCmd cid $ DsoRules_ConfirmAction with
      confirmer = sv
      action = ARC_DsoRules with
        dsoAction = SRARC_ConfirmSvOnboarding $ DsoRules_ConfirmSvOnboarding with
          newSvParty = sv4New
          newSvName = "sv4"
          newParticipantId = "sv4New-participant-id"
          reason = "because the sv4 owner still has the right to operate a node"
          newSvRewardWeight = 19 -- we use 19 as a dummy reward weight for testing
  executeAllConfirmedActions app
  [(svOnboardingConfirmedCid, _)] <- query @SvOnboardingConfirmed dso

  [(earliestRoundCid, _), (middleRoundCid, _), (latestRoundCid, _)] <- getActiveOpenRoundsSorted app

  [(amuletRulesCid, _)] <- query @AmuletRules dso
  [(dsoRulesCid, _)] <- query @DsoRules dso

  submitMulti [sv4New] [dso] $ exerciseCmd dsoRulesCid DsoRules_AddConfirmedSv with
    sv = sv4New
    svOnboardingConfirmedCid
    earliestRoundCid
    middleRoundCid
    latestRoundCid
    amuletRulesCid

  [(_, dsoRules)] <- query @DsoRules dso

  Map.keys dsoRules.svs === [sv1, sv2, sv3, sv4New]
  -- sv4 remaines offboarded
  Map.keys dsoRules.offboardedSvs === [sv4]

  -- check status reports
  checkSvContractInvariants app

  pure ()

testBootstrapDevNetWithNonZeroRound : Script ()
testBootstrapDevNetWithNonZeroRound = do
  (app, _, _) <- initDecentralizedSynchronizerWithNonZeroRound True 34
  [(_, earliestRound), (_, middleRound), (_, latestRound)] <- getActiveOpenRoundsSorted app
  earliestRound.round === Round 34
  middleRound.round === Round 35
  latestRound.round === Round 36
  pure ()

testBootstrapMainNetWithNonZeroRound : Script ()
testBootstrapMainNetWithNonZeroRound = do
  (app, _, _) <- initDecentralizedSynchronizerWithNonZeroRound False 34
  [(_, earliestRound), (_, middleRound), (_, latestRound)] <- getActiveOpenRoundsSorted app
  earliestRound.round === Round 34
  middleRound.round === Round 35
  latestRound.round === Round 36
  pure ()
