-- Copyright (c) 2024 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Splice.Scripts.TestDevelopmentFundCoupon where

import DA.Action (replicateA_)
import DA.Assert
import Daml.Script

import Splice.Amulet
import Splice.AmuletRules
import Splice.Scripts.Util

testAllocateDevelopmentFundCoupon : Script ()
testAllocateDevelopmentFundCoupon = do
  DefaultAppWithUsers{..} <- setupDefaultAppWithUsers
  -- Replace the default AmuletConfig with a 0.05% allocation for the development fund.
  -- No Fund manager configured.
  -- This configuration applies to the next open round.
  baseConfig <- getAmuletConfig app
  setAmuletConfig app baseConfig baseConfig with
    issuanceCurve = baseConfig.issuanceCurve with
      initialValue = baseConfig.issuanceCurve.initialValue with
        optDevelopmentFundPercentage = Some 0.05

  -- Run 3 issuances for the already open rounds without the development fund configuration.
  -- No development fund coupons are minted.
  replicateA_ 3 $ runNextIssuance app
  -- Mint one development fund coupon per issuance (two total).
  runNextIssuance app
  runNextIssuance app
  [(cid1, coupon1), (cid2, coupon2)] <- query @UnclaimedDevelopmentFundCoupon app.dso
  let
    totalAmount = coupon1.amount + coupon2.amount
    unclaimedDevelopmentFundCouponCids = [cid1, cid2]

  -- Unhappy - DevelopmentFundCoupon cannot be allocated without a Development Fund manager configured
  allocateDevelopmentFundCouponMustFail app alice.primaryParty bob.primaryParty totalAmount unclaimedDevelopmentFundCouponCids

  -- Set alice as the fund manager
  setAmuletConfig app baseConfig baseConfig with
    issuanceCurve = baseConfig.issuanceCurve with
      initialValue = baseConfig.issuanceCurve.initialValue with
        optDevelopmentFundPercentage = Some 0.05
        optDevelopmentFundManager = Some alice.primaryParty

  -- Unhappy - controller must be the configured Development Fund manager (alice)
  allocateDevelopmentFundCouponMustFail app bob.primaryParty bob.primaryParty totalAmount unclaimedDevelopmentFundCouponCids

  -- Unhappy - insufficient amount to cover the requested allocation
  allocateDevelopmentFundCouponMustFail app alice.primaryParty bob.primaryParty (totalAmount + 1.0) unclaimedDevelopmentFundCouponCids

  -- Happy - Development Fund coupon allocated with UnclaimedDevelopmentFundCoupon leftover
  let firstAllocationAmount = totalAmount - 1.0
  AmuletRules_AllocateDevelopmentFundCouponResult
    { developmentFundCouponCid, optUnclaimedDevelopmentFundCouponCid = Some leftOverUnclaimedDevelopmentFundCouponCid } <-
    allocateDevelopmentFundCoupon app alice.primaryParty bob.primaryParty firstAllocationAmount unclaimedDevelopmentFundCouponCids
  Some developmentFundCoupon <- queryContractId bob.primaryParty developmentFundCouponCid
  developmentFundCoupon === DevelopmentFundCoupon with
    dso = app.dso
    beneficiary = bob.primaryParty
    fundManager = alice.primaryParty
    amount = firstAllocationAmount

  -- Happy - Development Fund coupon allocated with no UnclaimedDevelopmentFundCoupon leftover
  AmuletRules_AllocateDevelopmentFundCouponResult { developmentFundCouponCid, optUnclaimedDevelopmentFundCouponCid = None } <-
    allocateDevelopmentFundCoupon app alice.primaryParty bob.primaryParty 1.0 [leftOverUnclaimedDevelopmentFundCouponCid]
  Some developmentFundCoupon <- queryContractId bob.primaryParty developmentFundCouponCid
  developmentFundCoupon === DevelopmentFundCoupon with
    dso = app.dso
    beneficiary = bob.primaryParty
    fundManager = alice.primaryParty
    amount = 1.0

  pure ()

allocateDevelopmentFundCouponMustFail
  : AmuletApp -> Party -> Party -> Decimal -> [ContractId UnclaimedDevelopmentFundCoupon]
  -> Script ()
allocateDevelopmentFundCouponMustFail app fundManager beneficiary amount unclaimedDevelopmentFundCouponCids = do
  [(amuletRulesCid, _)] <- query @AmuletRules app.dso
  submitMultiMustFail [fundManager] [app.dso] do
    exerciseCmd amuletRulesCid AmuletRules_AllocateDevelopmentFundCoupon with
      unclaimedDevelopmentFundCouponCids
      beneficiary
      amount
      fundManager
