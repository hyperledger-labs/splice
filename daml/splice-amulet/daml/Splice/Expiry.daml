-- Copyright (c) 2024 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

-- | Functions for computing amulet and lock expiry times and conditions.
module Splice.Expiry
  ( BoundedSet(..)
  , TimeLock(..)
  , isAmuletExpired
  -- for testing
  , amountExpiresAt
  , maxInt
  , maxDecimalDiv10
  , minTime
  , maxTime
  )
  where

import Splice.Api.Token.MetadataV1
import Splice.Amulet.TokenApiUtils

import Splice.Fees
import Splice.Types
import Splice.RelRound

import DA.Date
import DA.Time
import DA.TextMap as TextMap


-- Representing sets of values with uppper bounds
--------------------------------------------------

-- | A symbolic representation of a set of values of type `a` useful for computing upper-bounds.
data BoundedSet a
  = Singleton a     -- ^ `Singleton x` represents the singleton set `{x}`
  | AfterMaxBound
    -- ^ `AfterMaxBound` represents the set of all values larger than the maximal value that can be represented by type `a`
  deriving (Eq, Show)

-- Note: be careful when using this instance: `fmap (- 100) AfterMaxBound` will return
-- `AfterMaxBound`, but should actually return an error.
instance Functor BoundedSet where
  fmap f (Singleton x) = Singleton (f x)
  fmap _ AfterMaxBound = AfterMaxBound


-- BoundeSet Int
----------------

maxInt : Int
maxInt = maxBound

-- | Add a positive integer to a bounded set.
addBoundedInt : Int -> BoundedSet Int -> BoundedSet Int
addBoundedInt x
  | x < 0 = error $ "addBoundedInt: negative first operand " <> show x
  | otherwise = addBounded
  where
    addBounded AfterMaxBound = AfterMaxBound
    addBounded (Singleton y)
      | y <= maxInt - x = Singleton (x + y)
      | otherwise = AfterMaxBound

-- BoundedSet Decimal
---------------------


maxDecimalDiv10 : Decimal
maxDecimalDiv10 = 999999999999999999999999999.9999999999

-- | Divide two positive decimal numbers such that a division by a number between 0.0 and 1.0 does
-- not result in an overflow, but is instead captured as a bounded set.
--
-- We use `maxDecimalDiv10` as the upper bound to be on the safe side rounding shenanigans.
-- This is OK, as being overly conservative with returning `AfterMaxBound` is fine for the uses
-- of this function that we have.
boundedDivDecimal : Decimal -> Decimal -> BoundedSet Decimal
boundedDivDecimal x y
  | x <= 0.0 = error $ "boundedDivDecimal: negative first operand " <> show x
  | y <= 0.0 = error $ "boundedDivDecimal: negative second operand " <> show y
  | y < 1.0 && x > maxDecimalDiv10 * y = AfterMaxBound
  | otherwise = Singleton (x / y)

maxIntAsDecimal : Decimal
maxIntAsDecimal = intToDecimal maxBound

-- | Compute the ceiling of a decimal as a bounded int.
boundedCeiling : BoundedSet Decimal -> BoundedSet Int
boundedCeiling (Singleton x)
  | x > maxIntAsDecimal = AfterMaxBound
  | otherwise = Singleton (ceiling x)
boundedCeiling AfterMaxBound = AfterMaxBound


-- BoundedSet RelRound
-----------------------

-- | Add a BoundedSet of RelRound to a positive Round.
addBoundedRelRound : Round -> BoundedSet RelRound -> BoundedSet Round
addBoundedRelRound (Round x) y = Round <$> addBoundedInt x ((.diff) <$> y)

-- BoundedSet Time
------------------

-- | The smallest time point that can be represented by the `Time` type.
minTime = time (date 1 Jan 1) 0 0 0

-- | The largest time point that can be represented by the `Time` type.
maxTime = let oneSecMinusOneMicro = convertMicrosecondsToRelTime ( convertRelTimeToMicroseconds (seconds 1) - 1 )
          in addRelTime (time (date 9999 Dec 31) 23 59 59) oneSecMinusOneMicro


-- Amount expiry
----------------

-- | Compute number of rounds that the amount is expired with the specified rate
spentWithRatePerRound : RatePerRound -> Decimal -> BoundedSet RelRound
spentWithRatePerRound rate amount =
    RelRound <$> boundedCeiling roundsUntilExpired
  where
    roundsUntilExpired = amount `boundedDivDecimal` rate.rate

-- | Compute the round where the amulet expires. i.e. the amulet is fully expired.
amountExpiresAt : ExpiringAmount -> BoundedSet Round
amountExpiresAt e =
  e.createdAt `addBoundedRelRound` spentWithRatePerRound e.ratePerRound e.initialAmount


-- | A lock held by multiple parties until its expiry.
data TimeLock = TimeLock with
    holders: [Party]
    expiresAt: Time
    optContext : Optional Text
      -- ^ Short, human-readable description of the context in which the amulet is locked.
      -- Intended for wallets to inform the user about the reason for the lock.
      -- Consider what details to share about the context, as that might be private and
      -- the string here will be public information.
  deriving (Eq, Show, Ord)

-- | `isAmuletExpired openRound amuletAmount` computes whether the expiring `amuletAmount`
-- is definitely expired in case the `openRound` is open.
isAmuletExpired : Round -> ExpiringAmount -> Bool
isAmuletExpired round amuletAmount =
  let expiresAtRound = amountExpiresAt amuletAmount
  in case expiresAtRound of
    -- Expiring round cannot be represented by Round due to amulet amount is too large.
    -- This amulet will never expire unless it is broken into smaller amount.
    Singleton x -> x.number <= round.number
    AfterMaxBound -> False


-- AnyValue conversions
-----------------------

instance ToAnyValue TimeLock where
  toAnyValue lock = AV_Map $
    addOptionalAnyValue lockContextContextKey lock.optContext $
    TextMap.fromList
      [ (lockExpiresAtContextKey, toAnyValue lock.expiresAt),
        (lockHoldersContextKey, toAnyValue lock.holders)
      ]

instance FromAnyValue TimeLock where
  fromAnyValue (AV_Map values) = do
    let context = ChoiceContext with values
    expiresAt <- getFromContext @Time context lockExpiresAtContextKey
    holders <- getFromContext @[Party] context lockHoldersContextKey
    optContext <- lookupFromContext @Text context lockContextContextKey
    pure $ TimeLock with
      expiresAt
      holders
      optContext
  fromAnyValue _ = error "expected AV_Map for TimeLock"
